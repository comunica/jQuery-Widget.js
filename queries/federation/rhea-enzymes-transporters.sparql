# Select all Rhea reactions linked to protein sequences (enzymes and transporters) in UniProtKB
# Datasources: https://sparql.uniprot.org/sparql https://sparql.rhea-db.org/sparql

PREFIX rh: <http://rdf.rhea-db.org/>
PREFIX up: <http://purl.uniprot.org/core/>

# Query 12
# Select all Rhea reactions used to annotate enzyme sequences in UniProtKB
# return the number of UniProtKB entries
# Federated query using a service to UniProt SPARQL endpoint
# 
# This query corresponds to the Rhea website query:
# https://www.rhea-db.org/rhea?query=uniprot:*
# 
SELECT ?uniprotCount ?rhea ?accession ?equation 
WHERE {
  { 
  	SELECT ?rhea (count(?uniprot) as ?uniprotCount) {
      ?uniprot up:annotation/up:catalyticActivity/up:catalyzedReaction ?rhea . 
  	}
  	GROUP BY ?rhea
  }
  ?rhea rh:accession ?accession .
  ?rhea rh:equation ?equation .
}
